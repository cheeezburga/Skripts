options:
	offset: 5 # the size of the area the excavator digs
	enchants-per-tool: 3 # the number of levels of efficiency that the tool can have before upgrading to the next tier
	exponent: 1.25 # the amount that the price increases for tool leveling - ((no. of levels on tool)*10)^(exponent)
	autoswitch-price: 10000 # the price to unlock the autoswitch
	have-fuel-on-get: 8 # the amount of fuel the excavator should have by default
	require-fuel: true # set to false if you want excavators to automatically add fuel to never run out. will still show in lore of item

on load:
	set {excavator::offsets::1} to vector({@offset}, 0, {@offset})
	set {excavator::offsets::2} to vector({@offset}, 0, -{@offset})
	set {excavator::offsets::3} to vector(-{@offset}, 0, -{@offset})
	set {excavator::offsets::4} to vector(-{@offset}, 0, {@offset})

	# the tiers for the tool upgrades
	set {excavator::tools::1} to "wooden"
	set {excavator::tools::2} to "stone"
	set {excavator::tools::3} to "iron"
	set {excavator::tools::4} to "gold"
	set {excavator::tools::5} to "diamond"
	set {excavator::tools::6} to "netherite"

	# the number of blocks the excavator will mine for a specific fuel source
	# e.g. if a coal is input, 2 is added to the fuel (the excavator will mine 2 blocks per coal)
	set {excavator::fuels::coal} to 2
	set {excavator::fuels::charcoal} to 1

on skript stop:
	send "Backing up excavator inventories..." to console
	loop indexes of {excavators::*}:
		loop indexes of {excavators::%loop-value%::storage::*}:
			set {_index} to 1
			loop items in {excavators::%loop-value-1%::storage::%loop-value-2%}:
				set {excavators::%loop-value-1%::storage::%loop-value-2%::backup::%{_index}%} to loop-value-3
				add 1 to {_index}
	send "Backed up excavator inventories." to console

on skript start:
	send "Restoring excavator inventories..." to console
	loop indexes of {excavators::*}:
		loop indexes of {excavators::%loop-value%::storage::*}:
			set {_inv} to chest inventory with 6 rows named "Excavator Storage %loop-value-2%"
			loop {excavators::%loop-value-1%::storage::%loop-value-2%::backup::*}:
				add loop-value-3 to {_inv}
			set {excavators::%loop-value-1%::storage::%loop-value-2%} to {_inv}
			clear {excavators::%loop-value-1%::storage::%loop-value-2%::*}
	send "Restored excavator inventories." to console

on entity add to world:
	wait 1 tick
	if all:
		(byte tag "custom;excavator" of nbt of event-entity) is 1
		(metadata tag "isLooping" of event-entity) is not true
	then:
		doExcavatorLoop(event-entity)

local function placeExcavatorArea(p: player, b: block, e: entity):
	loop 4 times:
		set {_offsets::%loop-value%} to ({_b} ~ {excavator::offsets::%loop-value%})
	set {_type} to 1
	loop 4 times:
		if loop-value < 4:
			set {_blocks::*} to (blocks within {_offsets::%loop-value%} and {_offsets::%loop-value + 1%})
		else:
			set {_blocks::*} to (blocks within {_offsets::%loop-value%} and {_offsets::1})
		loop {_blocks::*}:
			set (block at loop-value-2) to yellow concrete if ({_type} is 1) else black concrete
			set {_type} to 0 if ({_type} is 1) else 1
			add (block at loop-value-2) to {_total::*}
	set {excavators::%uuid of {_e}%::area::*} to {_total::*}

local function spawnExcavator(p: player, b: block, item: item) :: entity:
	set {_tool} to (compound tag "tool" of nbt of {_item})
	set {_storages} to (int tag "storages" of nbt of {_item})
	set {_autoswitch} to (byte tag "autoswitch" of nbt of {_item})
	set {_fuel} to (int tag "fuel" of nbt of {_item})
	spawn block display at ((exact location of {_b}) ~ vector(0,3,0)):
		set display block data of entity to lodestone[]
		set teleport duration of entity to 2
		set (byte tag "custom;excavator" of nbt of entity) to 1
		set (int tag "custom;placedX" of nbt of entity) to floor(x-coordinate of {_b})
		set (int tag "custom;miningY" of nbt of entity) to (y-coordinate of {_b})
		set (int tag "custom;placedZ" of nbt of entity) to floor(z-coordinate of {_b})
		set (uuid tag "custom;placer" of nbt of entity) to {_p}
		set {excavators::%uuid of entity%::tool} to nbt item of {_tool}
		set {excavators::%uuid of entity%::autoswitch} to {_autoswitch}
		set {excavators::%uuid of entity%::fuel} to {_fuel}
		loop {_storages} times:
			set {_inv} to chest inventory with 6 rows named "Excavator Storage %loop-value%"
			set {excavators::%uuid of entity%::storage::%loop-value%} to {_inv}
		set {_d} to entity
	return {_d}

local function spawnInteraction(e: entity) :: entity:
	spawn interaction at ({_e} ~ vector(0.5,0,0.5)):
		set (byte tag "custom;excavatorInteraction" of nbt of entity) to 1
		set (uuid tag "custom;excavator" of nbt of entity) to {_e}
		set (uuid tag "custom;interaction" of nbt of {_e}) to entity
		set {_d} to entity
	return {_d}

local function getBlocksOnY(e: entity) :: blocks:
	set {_y} to (int tag "custom;miningY" of nbt of {_e})
	set {_x} to (int tag "custom;placedX" of nbt of {_e})
	set {_z} to (int tag "custom;placedZ" of nbt of {_e})
	set {_center} to location({_x}, {_y}, {_z}, (world of {_e}))
	set {_tl} to block at ({_center} ~ ({excavator::offsets::1}++vector(-1,0,-1)))
	set {_br} to block at ({_center} ~ {excavator::offsets::3}++vector(1,0,1))
	return ((blocks within {_tl} and {_br}) where [input is not (air, black concrete, yellow concrete, water or lava)])

local function getNextLocation(e: entity) :: location:
	set {_blocks::*} to getBlocksOnY({_e})
	#broadcast "blocks %{_blocks::*}%"
	set {_next} to exact location of (first element out of {_blocks::*})
	return {_next}

local function drawLine(e: entity):
	set {_loc1} to {_e} ~ vector(0.5,0,0.5)
	set {_loc2} to {_loc1} ~ vector(0,-2,0)
	set {_v} to vector from {_loc1} to {_loc2}
	set vector length of {_v} to 0.25 # step size
	set {_i} to 1
	loop floor((distance between {_loc1} and {_loc2}) / 0.25) times:
		draw 1 flame at {_loc1} with extra 0
		set {_loc1} to {_loc1} ~ {_v}

local function addToInventory(e: entity, items: items) :: boolean:
	loop (indexes of {excavators::%uuid of {_e}%::storage::*}):
		if {excavators::%uuid of {_e}%::storage::%loop-value%} has space for {_items::*}:
			add {_items::*} to {excavators::%uuid of {_e}%::storage::%loop-value%}
			return true
	return false

local function doExcavatorLoop(e: entity):
	set (metadata tag "isLooping" of {_e}) to true
	wait 1 tick
	set {_interaction} to ((uuid tag "custom;interaction" of nbt of {_e}) parsed as entity)
	while {excavators::%uuid of {_e}%::fuel} > 0:
		set {_next} to getNextLocation({_e})
		while {_next} is not set:
			subtract 1 from (int tag "custom;miningY" of nbt of {_e})
			set {_next} to getNextLocation({_e})
		teleport {_e} to {_next} ~ vector(0,3,0)
		teleport {_interaction} to ({_e} ~ vector(0.5,0,0.5))
		wait 2 ticks # the same as the teleport duration of {_e}. gives it time to interpolate
		if {excavators::%uuid of {_e}%::autoswitch} is 1:
			if isPreferredTool({excavators::%uuid of {_e}%::tool}, (block at {_next})) is false:
				if isPreferredTool(getEquivalentTool({excavators::%uuid of {_e}%::tool}), (block at {_next})) is true: # if more tools (axes, hoes, etc), this would need to change
					switchTool({_e})
		set {_hardness} to block hardness of (block at {_next})
		set {_speed} to (block at {_next}).getDestroySpeed({excavators::%uuid of {_e}%::tool}, true)
		set {_time} to {_hardness} / {_speed}
		set {_time} to "%{_time}% seconds" parsed as timespan
		set {_wait} to "%ticks of {_time}% ticks" parsed as timespan
		set {_progress} to 0
		loop 10 times:
			if {_e} is alive:
				add 0.1 to {_progress}
				drawLine({_e})
				make all players see damage of (block at {_next}) as {_progress} from {_e}
				wait {_wait}
			else:
				make all players see damage of (block at {_next}) as 0 from {_e}
				exit 2 loops
		set {_drops::*} to drops of (block at {_next}) using {excavators::%uuid of {_e}%::tool}
		set {_added} to addToInventory({_e}, {_drops::*})
		set (block at {_next}) to air
		if {_added} is true:
			set glowing of {_e} to false
		else:
			drop {_drops::*} at {_next} ~ vector(0,0.5,0)
			set glowing of {_e} to true
			set glow color override of {_e} to bukkitColor(1,255,0,0)
		subtract 1 from {excavators::%uuid of {_e}%::fuel}
		add 1 to {excavators::%uuid of {_e}%::fuel} if ({@use-fuel} is false)
	set (metadata tag "isLooping" of {_e}) to false

local function getExcavatorItem() :: item:
	set {_i} to lodestone named "&f&lExcavator" with lore "&7Place this anywhere to set up an excavator!"
	set {_tool} to ("%{excavator::tools::1}% pickaxe" parsed as itemtype)
	set {_toolLore} to "&b%type of {_tool}%" if ({_tool} is enchanted) else "&f%type of {_tool}%"
	set (byte tag "excavator" of nbt of {_i}) to 1
	set (compound tag "tool" of nbt of {_i}) to (full nbt of {_tool})
	set (int tag "storages" of nbt of {_i}) to 1
	set (byte tag "autoswitch" of nbt of {_i}) to 0
	set (int tag "fuel" of nbt of {_i}) to {@have-fuel-on-get}
	set line 2 of lore of {_i} to "&7Current tool: %{_toolLore}%"
	set line 3 of lore of {_i} to "&7No. of storages: &f1"
	set line 4 of lore of {_i} to "&7Autoswitcher: &cnot unlocked"
	set line 5 of lore of {_i} to "&7Fuel: &f{@have-fuel-on-get}"
	return {_i}

local function getExcavatorItemFromEntity(e: entity) :: item:
	set {_i} to getExcavatorItem()
	set {_storages} to size of (indexes of {excavators::%uuid of {_e}%::storage::*})
	set {_tool} to {excavators::%uuid of {_e}%::tool}
	set {_toolLore} to "&b%type of {_tool}%" if ({_tool} is enchanted) else "&f%type of {_tool}%"
	set {_autoswitch} to "&aunlocked" if ({excavators::%uuid of {_e}%::autoswitch} is 1) else "&cnot unlocked"
	set (byte tag "excavator" of nbt of {_i}) to 1
	set (compound tag "tool" of nbt of {_i}) to (full nbt of {_tool})
	set (byte tag "autoswitch" of nbt of {_i}) to {excavators::%uuid of {_e}%::autoswitch}
	set (int tag "storages" of nbt of {_i}) to {_storages}
	set (int tag "fuel" of nbt of {_i}) to {excavators::%uuid of {_e}%::fuel}
	set line 2 of lore of {_i} to "&7Current tool: %{_toolLore}%"
	set line 3 of lore of {_i} to "&7No. of storages: &f%{_storages}%"
	set line 4 of lore of {_i} to "&7Autoswitcher: &f%{_autoswitch}%"
	set line 5 of lore of {_i} to "&7Fuel: &f%{excavators::%uuid of {_e}%::fuel}%"
	return {_i}

local function killExcavator(p: player, e: entity):
	set {_excavator} to ((uuid tag "custom;excavator" of nbt of {_e}) parsed as entity)
	loop {excavators::%uuid of {_excavator}%::area::*}:
		set (block at loop-value) to air
	loop indexes of {excavators::%uuid of {_excavator}%::storage::*}:
		loop items in {excavators::%uuid of {_excavator}%::storage::%loop-value%}:
			drop loop-value-2 at {_excavator}
	set {_item} to getExcavatorItemFromEntity({_excavator})
	clear {excavators::%uuid of {_excavator}%::*}
	delete {excavators::%uuid of {_excavator}%}
	give or drop {_item} to {_p} # change to add cooldown on when other players can pick up
	set {_placer} to ((uuid tag "custom;placer" of nbt of {_excavator}) parsed as player)
	kill {_e} and {_excavator}
	send action bar "&aAn excavator you placed has been destroyed." to {_placer}

local function getType(currentTool: item) :: string:
	return ("pickaxe" if ({_currentTool} is any pickaxe) else "shovel")

local function getUpgradeToolPrice(upgradeTool: item) :: int:
	loop indexes of {excavator::tools::*}:
		if plain {_upgradeTool} is ("%{excavator::tools::%loop-value%}% %getType({_upgradeTool})%" parsed as itemtype):
			add (loop-value parsed as integer) to {_i}
	loop ({_i} - 1) times:
		add 3 to {_i}
	add (level of efficiency on {_upgradeTool}) to {_i}
	return ceil(({_i}^{@exponent})*100)

local function getUpgradeTool(currentTool: item) :: item:
	set {_enchant} to (level of efficiency on {_currentTool})
	if {_enchant} is not set:
		set (level of efficiency on {_currentTool}) to 1
		return {_currentTool}
	else if {_enchant} < {@enchants-per-tool}:
		set (level of efficiency on {_currentTool}) to ({_enchant} + 1)
		return {_currentTool}
	else:
		loop {excavator::tools::*}:
			set {_loopTool} to ("%loop-value% %getType({_currentTool})%" parsed as itemtype)
			if (plain {_currentTool}) is {_loopTool}:
				set {_next} to true
				continue
			return {_loopTool} if ({_next} is set)
		return {_currentTool} # max tool

local function getEquivalentTool(currentTool: item) :: item:
	set {_currentType} to getType({_currentTool})
	set {_newType} to "shovel" if ({_currentType} is "pickaxe") else "pickaxe"
	set {_material} to (first element out of ("%{_currentTool}%" split at {_currentType}))
	set {_newTool} to ("%{_material}%%{_newType}%" parsed as itemtype)
	set (level of efficiency on {_newTool}) to ((level of efficiency on {_currentTool}) ? 0)
	return {_newTool}

local function upgradeTool(p: player, e: entity) :: boolean:
	set {_currentTool} to {excavators::%uuid of {_e}%::tool}
	set {_upgradedTool} to getUpgradeTool({_currentTool})
	if {_currentTool} is {_upgradedTool}:
		send action bar "&cAlready maxed out the tool." to {_p}
		return false
	set {_price} to getUpgradeToolPrice({_upgradedTool})
	if {_p}'s balance >= {_price}:
		subtract {_price} from {_p}'s balance
		set {excavators::%uuid of {_e}%::tool} to {_upgradedTool}
		send action bar "&aUpgraded your excavator's tool to %{_upgradedTool}%." to {_p}
		return true
	else:
		send action bar "&cNot enough money to buy this upgrade." to {_p}
	return false

local function isPreferredTool(currentTool: item, b: block) :: boolean:
	set {_type} to getType({_currentTool})
	return true if ({_b} is tagged with (minecraft block tag "minecraft:mineable/%{_type}%")) else false

local function switchTool(e: entity) :: boolean:
	set {_newTool} to getEquivalentTool({excavators::%uuid of {_e}%::tool})
	set {excavators::%uuid of {_e}%::tool} to {_newTool}

local function openExcavatorStorage(p: player, e: entity, inv: integer):
	close {_p}'s inventory
	open {excavators::%uuid of {_e}%::storage::%{_inv}%} to {_p}

local function getNewStoragePrice(e: entity) :: number:
	set {_storages} to (size of (indexes of {excavators::%uuid of {_e}%::storage::*}))
	set {_next} to {_storages} + 1
	return ceil(({_next}*10)^2.5)

local function unlockNewStorage(p: player, e: entity):
	set {_price} to getNewStoragePrice({_e})
	if {_p}'s balance >= {_price}:
		set {_storages} to (size of (indexes of {excavators::%uuid of {_e}%::storage::*}))
		set {_inv} to chest inventory with 6 rows named "Excavator Storage %{_storages} + 1%"
		set {excavators::%uuid of {_e}%::storage::%{_storages} + 1%} to {_inv}
		send action bar "&aUnlocked a new storage for your excavator" to {_p}
	else:
		send action bar "&cNot enough money to buy this upgrade." to {_p}

local function cycleStorageToOpen(p: player, e: entity):
	set {_currentToOpen} to (metadata tag "storageToOpen" of {_p})
	set {_storages} to (size of (indexes of {excavators::%uuid of {_e}%::storage::*}))
	set (metadata tag "storageToOpen" of {_p}) to ({_currentToOpen} + 1) if (({_currentToOpen} + 1) <= {_storages}) else 1

local function unlockAutoswitcher(p: player, e: entity) :: boolean:
	if {excavators::%uuid of {_e}%::autoswitch} is 0:
		if {_p}'s balance >= {@autoswitch-price}:
			set {excavators::%uuid of {_e}%::autoswitch} to 1
			send action bar "&aUnlocked the autoswitcher for that excavator." to {_p}
			return true
		else:
			send action bar "&cNot enough money to buy this upgrade." to {_p}
			return false
	else:
		send action bar "&cAlready unlocked the autoswitcher for this excavator." to {_p}
		return false

local function openFuel(p: player, e: entity):
	close {_p}'s inventory
	set (metadata tag "fuelFor" of {_p}) to {_e}
	create gui with virtual chest inventory with 3 rows named "Excavator Fuel":
		format gui slot ((integers from 0 to 12) and (integers from 14 to 26)) with black stained glass pane named "&fDeposit fuel here" with lore ("&7Drop your coal (%{excavator::fuels::coal}%) or charcoal (%{excavator::fuels::charcoal}%) in the middle slot." and "&cWarning: &7this cannot be undone.")
	open last gui to {_p}

local function increaseFuelAmount(amount: integer, type: itemtype) :: integer:
	loop (indexes of {excavator::fuels::*}):
		set {_index} to loop-value parsed as itemtype
		if {_index} is {_type}:
			set {_return} to ({_amount} * {excavator::fuels::%loop-value%})
	return {_return}

on inventory click:
	if (metadata tag "fuelFor" of player) is set:
		set {_e} to (metadata tag "fuelFor" of player)
		if holder of event-inventory is not player:
			if index of clicked slot is 13:
				cancel event
				if (indexes of {excavator::fuels::*}) contains ("%type of player's cursor slot%"):
					if click type is left mouse button:
						set {_amount} to (item amount of player's cursor slot)
					else if click type is right mouse button:
						set {_amount} to 1
					set {_increase} to increaseFuelAmount({_amount}, (type of player's cursor slot))
					subtract {_amount} from (item amount of player's cursor slot)
					add {_increase} to {excavators::%uuid of {_e}%::fuel}
					doExcavatorLoop({_e}) if (metadata tag "isLooping" of {_e}) is false
		else if click type is left mouse button with shift:
			cancel event
			if (indexes of {excavator::fuels::*}) contains ("%type of clicked slot%"):
				add increaseFuelAmount((item amount of clicked slot), (type of clicked slot)) to {excavators::%uuid of {_e}%::fuel}
				set clicked slot to air
				doExcavatorLoop({_e}) if (metadata tag "isLooping" of {_e}) is false

on inventory close:
	if (metadata tag "fuelFor" of player) is set:
		wait 1 tick
		clear (metadata tag "fuelFor" of player)

local function openExcavatorOptions(p: player, e: entity):
	close {_p}'s inventory
	set {_tool} to {excavators::%uuid of {_e}%::tool}
	set {_c} to "&b" if ({_tool} is enchanted) else "&f"
	set {_toolName} to ("%{_tool}%" in proper case)
	set {_toolName} to "%{_c}%%{_toolName}% &f&l(%(level of efficiency on {_tool}) ? 0%)"
	set {_upgradeToolPrice} to getUpgradeToolPrice(getUpgradeTool({_tool}))
	set {_unlockStoragePrice} to getNewStoragePrice({_e})
	set (metadata tag "storageToOpen" of {_p}) to 1
	set {_autoswitch} to "&aunlocked" if ({excavators::%uuid of {_e}%::autoswitch} is 1) else "&cnot unlocked &7(click to purchase for &6$10,000&7)"
	create gui with virtual chest inventory with 3 rows named "Excavator Options":
		format gui slot 10 with {_tool} named {_toolName} with lore ("&7Upgrade price: &6$%{_upgradeToolPrice}%", "", "&7Click to upgrade!" and "&7Right click to switch to alternative tool.") with all flags hidden:
			if gui click type is left mouse button:
				set {_upgraded} to upgradeTool({_p}, {_e})
				openExcavatorOptions({_p}, {_e}) if {_upgraded} is true
			else if gui click type is right mouse button:
				switchTool({_e})
				openExcavatorOptions({_p}, {_e})
		format gui slot 12 with chest named "&fStorage" with lore ("&7Click to open &fStorage %(metadata tag "storageToOpen" of {_p})%&7!", "", "&7Right click to cycle." and "&7Middle click to unlock another for &6$%{_unlockStoragePrice}%&7."):
			if gui click type is left mouse button:
				openExcavatorStorage({_p}, {_e}, (metadata tag "storageToOpen" of {_p}))
			else if gui click type is right mouse button:
				cycleStorageToOpen({_p}, {_e})
				set (line 1 of lore of gui item) to "&7Click to open &fStorage %(metadata tag "storageToOpen" of {_p})%&7!"
			else if gui click type is middle mouse button:
				unlockNewStorage({_p}, {_e})
				set {_unlockStoragePrice} to getNewStoragePrice({_e})
				set (line 4 of lore of gui item) to "&7Middle click to unlock another for &6$%{_unlockStoragePrice}%&7."
		format gui slot 14 with arrow named "&fAuto Switcher" with lore ("&7Currently: %{_autoswitch}%"):
			set {_unlocked} to unlockAutoswitcher({_p}, {_e})
			openExcavatorOptions({_p}, {_e}) if {_unlocked} is true
		format gui slot 16 with furnace named "&fFuel" with lore ("&7Currently: &f%{excavators::%uuid of {_e}%::fuel}%" and "&7Click to deposit some more."):
			openFuel({_p}, {_e})
		loop ((3*9)-4) times:
			format next gui slot with black stained glass pane
	open last gui to {_p}

on place:
	if (nbt of player's tool) has tag "excavator":
		cancel event
		set {_e} to spawnExcavator(player, event-block, player's tool)
		set {_i} to spawnInteraction({_e})
		remove 1 of player's tool from player
		placeExcavatorArea(player, event-block, {_e})
		doExcavatorLoop({_e})

on damage of interaction:
	if (byte tag "custom;excavatorInteraction" of nbt of victim) is 1:
		killExcavator(attacker, victim)

on right click on interaction:
	if (byte tag "custom;excavatorInteraction" of nbt of event-entity) is 1:
		set {_excavator} to ((uuid tag "custom;excavator" of nbt of event-entity) parsed as entity)
		openExcavatorOptions(player, {_excavator})

command /excavator [<text>]:
	trigger:
		if arg-1 is not set:
			send "&aExcavator by cheezburga, concept from YouCanDream"
		else:
			if arg-1 is "get" or "give":
				give getExcavatorItem() to player
				send action bar "&aGave you an excavator." to player
			else:
				send "&cNot a valid argument."